//   GITCHECK
//
//   Show the short GIT status for all GIT repositories at or below the current
//   directory.  This can be useful to quickly see which respositories have
//   uncommitted changes.
//
var new nrepo integer

////////////////////////////////////////////////////////////////////////////////
//
//   Subroutine REPO dir
//
//   Handle the GIT repository DIR.  DIR is the full pathname of the repository.
//
subroutine repo
  set nrepo [+ nrepo 1]
  dir [arg 1]
  show [dir]
  run "git status -s"
  endsub

////////////////////////////////////////////////////////////////////////////////
//
//   Subroutine DOTREE dir
//
//   Find and handle GIT repositories in the tree DIR.  DIR is a string.
//
subroutine dotree
  loop dir [arg 1] dir
    if [= [dent] ".git"]
      then
        call repo [dent dir] //process this repository
        quit
      else
        call dotree:+1 [dent tnam]
      endif
    endloop
  endsub

////////////////////////////////////////////////////////////////////////////////
//
//   Start of main routine.
//
show
call dotree "."
show
show nrepo " repositories found."
