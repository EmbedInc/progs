//   GITCHECK
//
//   Show the short GIT status for all GIT repositories at or below the current
//   directory.  This can be useful to quickly see which respositories have
//   uncommitted changes.
//
const tempfile string = [tnam [evar "temp"] "/gitcheck.txt"]

var new nrepo integer
var new nchanged integer

////////////////////////////////////////////////////////////////////////////////
//
//   Subroutine REPO dir
//
//   Handle the GIT repository DIR.  DIR is the full pathname of the repository.
//
subroutine repo
  set nrepo [+ nrepo 1]
  dir [arg 1]
  run "cmd /c git status -s >" tempfile
  if [> [file len tempfile] 0] then
    set nchanged [+ nchanged 1]
    show [dir]
    run "copya -in """ tempfile """ -list"
    endif
  endsub

////////////////////////////////////////////////////////////////////////////////
//
//   Subroutine DOTREE dir
//
//   Find and handle GIT repositories in the tree DIR.  DIR is a string.
//
subroutine dotree
  loop dir [arg 1] dir
    if [= [dent] ".git"]
      then
        call repo [dent dir] //process this repository
        quit
      else
        call dotree:+1 [dent tnam]
      endif
    endloop
  endsub

////////////////////////////////////////////////////////////////////////////////
//
//   Start of main routine.
//
show
call dotree "."
show
show nrepo " repositories checked, " nchanged " with changes found."
if [> nchanged 0] then
  del tree tempfile
  endif
