//   SHOWNEW [date]
//
//   Show all the files in the current tree that are newer than DATE.  The
//   format for DATE is YYYY/MM/DD.HH:MM:SS.  Anything past the first slash may
//   be omitted.
//
//   The default for DATE is the start of the previous day.
//
const dtmfmt string = 'local year str "/" mnum str "/" day str "." hour str ":" min str ":" sec'
const now time = [now]
var new thdate time
var new t1 time
var new t2 time

////////////////////////////////////////////////////////////////////////////////
//
//   Subroutine DODIR
//
//   Process all the files in the current directory and any of its
//   sub-directories.
//
subroutine dodir
  var local olddir string = [dir]

  loop dir "."
    pick one by [dent type]
      option "DIR"
        dir [dent]
        call dodir:+1        //process subdirectory recursively
        dir olddir
      option "FILE"
        if [> [dent dtm] thdate] then //newer than the threshold ?
          show [date [dent dtm] [chars dtmfmt]] " " [dent tnam]
          endif
      endpick
    endloop

  endsub

////////////////////////////////////////////////////////////////////////////////
//
//   Start of main routine.
//
if [exist 1 arg]
  then
    set t1 [chars [date now local]]
    set thdate [+ [arg 1] [- now t1]]
  else
    set t1 [chars [date now local year str "/" mnum str "/" day]]
    set t2 [chars [date now local]]
    set thdate [- now [* 24 60 60] [- t2 t1]]
  endif

call dodir
